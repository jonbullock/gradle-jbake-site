buildscript {
    repositories {
        jcenter()
    }
  
    dependencies {
        classpath 'me.champeau.gradle:jbake-gradle-plugin:0.2'
        classpath 'org.jbake:jbake-core:2.5.1'

        classpath 'org.asciidoctor:asciidoctorj:1.5.4.1'
        classpath 'org.pegdown:pegdown:1.6.0'

        classpath 'org.freemarker:freemarker:2.3.25-incubating'

        classpath 'org.codehaus.groovy:groovy:2.4.7'
        classpath 'org.codehaus.groovy:groovy-templates:2.4.7'

        classpath 'org.thymeleaf:thymeleaf:3.0.2.RELEASE'

        classpath 'de.neuland-bfi:jade4j:1.2.3'
    }
}

plugins {
    id "org.ajoberstar.github-pages" version "1.6.0"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'me.champeau.jbake'

jbake {
    input = file('src/main/jbake')
    output = file('output')
    clearCache = true
}

def siteDir = file('output')

githubPages {
    repoUri = 'https://github.com/jonbullock/jonbullock.github.io.git'
    targetBranch = 'master'

    pages {
        from(file("$siteDir")) {
            into '.'
        }
    }
  
    credentials {
        // Use token stored as env variable
        username = System.getenv('GH_TOKEN')
        // Apparently password can't be null or '' 
        password = '\n'
    }
}

publishGhPages.dependsOn jbake

task publish(dependsOn: publishGhPages, group: 'render', description: "Publishes buildDir to Github Pages on 'master' branch")
